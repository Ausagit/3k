#nop gen_onkill.tin
/* 
	On kill action
	onk/onk1		- show your on kill action
	onk/onk1 clear	- clear your onkill action
	onk <action>	- repeated every round until turned off
	onk1 <action>	- fires once on the next kill then is cleared
	
	Example: .stop;onk1 cdb 100 .resume
*/

#class {gen_onkill} {kill}
#class {gen_onkill} {open}

#variable {gen_onkill_module} {1}
#variable {onkill} {}
#variable {onkill_os} {0}

#alias {onk}
{
	#if {"%0" == ""} {
		#if {"$onkill" == ""} {
			#echo {<fca>You have no onkill command.<088>}
		} {
			#if {$onkill_os} {
				#echo {<fca>Onkill oneshot is set to %s.<088>} {$onkill}
			} {
				#echo {<fca>Onkill is set to %s.<088>} {$onkill}
			}
		}
	}; #elseif {"%0" == "clear"} {
		#variable {onkill} {};
		#variable {onkill_os} {0};
		#echo {<fca>Onkill cleared.<088>}
	}; #else {
        #variable {onkill} {%0};
		#variable {onkill_os} {0};
		#echo {<fca>Onkill set to %s.<088>} {$onkill}
	}
}
#alias {onk1}
{
	#if {"%0" == ""} {
        #if {"$onkill" == ""} {
			#echo {<fca>You have no onkill command.<088>}
		} {
            #if {$onkill_os} {
                #echo {<fca>Onkill oneshot is set to %s.<088>} {$onkill}
            } {
                #echo {<fca>Onkill is set to %s.<088>} {$onkill}
            }
        }
	}; #elseif {"%0" == "clear"} {
		#variable {onkill} {};
		#variable {onkill_os} {0};
		#echo {<fca>Onkill cleared.<088>}		
	}; #else {
        #variable {onkill} {%0};
        #variable {onkill_os} {1};
        #echo {<fca>Onkill oneshot is set to %s.<088>} {$onkill}
    }
}

#alias {onkillrun}
{
    #if {"$onkill" != ""} {
        $onkill;
        #if {$onkill_os} {
            #variable {onkill} {};
            #variable {onkill_os} {0}
        }
    }
}

#class {gen_onkill} {close}
