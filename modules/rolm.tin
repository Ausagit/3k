#nop rolm.tin
/*
Module for handling rolm events and charge count


Define the alias below where required to handle the rolm events.

#alias {on_rolm_rune_recharged}
{
    #nop %1 is the rune name that has just recharged;
}

#alias {on_rolm_rune_activated}
{
    #nop %1 is the rune name that was activated;
}

#alias {on_rolm_rune_failed}
{
    #nop %1 is the rune name that failed to activate;
}

#alias {on_rolm_changed}
{
    #switch {"%1"} {
        #case "edged" {

        };
        #case "blunt" {

        };
        #case "fire" {

        };
        #case "ice" {

        };
        #case "acid" {

        };
        #case "electric" {

        };
        #case "mind" {

        };
        #case "energy" {

        };
        #case "poison" {

        };
        #case "radiation" {

        }
    }
}
*/

#nop change this to the room vnum that you usually anchor in, use setwr to change it manually
#variable {rolm_module} {1}
#variable {winchroom} {152}
#variable {rolm[type]} {blunt}
#variable {rolm[charges_long]} {}
#variable {rolm[charges]} {0}

#alias {setwr} {#variable {winchroom} {$roomvnum}}

#action {^Warming in your grasp, %1 twinkling}
{
    #variable {rolm[charges_long]} {%1};
    #local {digits} {};
    #local {words} {};
    #local {res} {0};

    #local digits[one] 1;
    #local digits[two] 2;
    #local digits[three] 3;
    #local digits[four] 4;
    #local digits[five] 5;
    #local digits[six] 6;
    #local digits[seven] 7;
    #local digits[eight] 8;
    #local digits[nine] 9;
    #local digits[ten] 10;
    #local digits[eleven] 11;
    #local digits[twelve] 12;
    #local digits[thirteen] 13;
    #local digits[fourteen] 14;
    #local digits[fifteen] 15;
    #local digits[sixteen] 16;
    #local digits[seventeen] 17;
    #local digits[eighteen] 18;
    #local digits[nineteen] 19;
    #local digits[twenty] 20;
    #local digits[thirty] 30;
    #local digits[forty] 40;
    #local digits[fifty] 50;
    #local digits[sixty] 60;
    #local digits[seventy] 70;
    #local digits[eighty] 80;
    #local digits[ninety] 90;
    #local digits[hundred] 100;

    #replace {rolm[charges_long]} { } {;};
    #replace {rolm[charges_long]} {-} {;};
    #list {words} {create} {$rolm[charges_long]};

    #foreach {$words[%*]} {word} {
        #switch {"$word"} {
            #case {"hundred"} {
                #math {res} {$res * 100}
            };
            #default {
                #math {res} {$res + $digits[$word]}
            }
        }
    };

    #variable {rolm[charges]} {$res}
}

#action {^The diamond rune flashes briefly on the artifact.}
{
	#math {rolm[charges]} {$rolm[charges] -1}
}

#action {^The diamond rune flashes twice briefly on the artifact.}
{
	#math {rolm[charges]} {$rolm[charges] -2}
}

#action {^The diamond rune flashes thrice briefly on the artifact.}
{
	#math {rolm[charges]} {$rolm[charges] -3}
}

#action {^Divine energy dances up and down the artifact, welling with power renewed.}
{
    touch diamond rune
}

#nop #action {^Your protective field has dissipated.} {update_chat %0}
#action {^Your immortality has faded.} {update_chat %0}

#action {^You wield HolyScythe.}            {#variable {rolm[type]} {edged};on_rolm_changed edged}
#action {^You wield Rod of Lordly Might.}   {#variable {rolm[type]} {blunt};on_rolm_changed blunt}
#action {^You wield DragonBlade.}           {#variable {rolm[type]} {fire};on_rolm_changed fire}
#action {^You wield GlacialAxe.}            {#variable {rolm[type]} {ice};on_rolm_changed ice}
#action {^You wield AcidSlinger.}           {#variable {rolm[type]} {acid};on_rolm_changed acid}
#action {^You wield ArcLance.}              {#variable {rolm[type]} {electric};on_rolm_changed electric}
#action {^You wield PsiMace.}               {#variable {rolm[type]} {mind};on_rolm_changed mind}
#action {^You wield SolarFlail.}            {#variable {rolm[type]} {energy};on_rolm_changed energy}
#action {^You wield VenomClaw.}             {#variable {rolm[type]} {poison};on_rolm_changed poison}
#action {^You wield PhotonSphere.}          {#variable {rolm[type]} {radiation};on_rolm_changed radiation}

#action {^You touch the %1 rune on the artifact and hear a soft *click*}
{
    #format {touching_rune} {%l} {%1}
}

#action {^Nothing happens after touching that activated rune}
{
    on_rolm_rune_failed $touching_rune
}

#action {^Upon the %1, the %2 rune glows brightly again.}
{
    on_rolm_rune_recharged %2
}

#action {^   whispering a dark prayer to the provenance of an ancient god...}
{
    on_rolm_rune_activated raven
}

#action {^A charge of emerald energy from the relic fractally crawls across your head.}
{
    on_rolm_rune_activated owl
}

#action {^The light fades and you materialize elsewhere....}
{
    #if {"$winchroom" != ""} {
        #map goto $winchroom
    }
}

#action {^A small golden strand falls from the relic and imbeds itself into the ground.}
{
    #nop check the touching rune to prevent changing anchor point when someone else does it;
    #if {"$touching_rune" == "anchor"} {
        setwr
    }
}

#action {^The %* warms slightly in your grip.}
{
    #nop angel rune;
    #nop if this message doesn't trigger, swap to the other rune;
}

