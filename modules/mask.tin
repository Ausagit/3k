#nop mask.tin
/*
	Script for handling corpses with or without The mask of Gemino Mortis.
	This script allows the mask to be active or inactive, only reacting to mask
	events when the reclaim (mu) script is running. 
	Use the mu alias from update_status when your desired conditions are met to
	trigger a reclaim. Can be easily modified to suit your guild preferences.

	Change the order of corpse usage in the _select_corpse to your liking.

	Note, guild followers are not included as they can be unreliable due to no port zones etc.
	If you use your follower to carry corpses, add some code elsewhere to shuffle corpses.

	Variables:
	$mask_on			- 0: off, 1: own mask, 2: anyone's mask
	$recently_reclaimed	- prevent simultaneous reclaims and allow for GP to settle
	$mask_check			- set to 1, if 1 after 3s during a reclaim, mask failed, reclaim
	$ses_name			- your name from Inix's tintin setup
	$guild				- your guild name from Inix's tintin setup

	Usage:
	mask on			- Trigger from your mask only. Glyph is forced off when not reclaiming.
	mask all		- Trigger from anyone's mask. Manage the glyph manually.
	mask off		- Default, reclaim instantly.
	mask			- Show the mask and glyph status

	Example code for update_status (or hp trigger, etc):
	#if {($recently_reclaimed == 0) && ($enemy[hp] > 1) && ($my[gp1][perc] < 40)} {
		#if {$mask_module} {
			#delay {reclaim} {mu} {0.1}
		} {
			#delay {reclaim} {
				#nop do your basic guild corpse commands;
			} {0.1}
		}
	};
*/
#class {mask} {kill}
#class {mask} {open}

#variable {mask_module} {1}
#variable {mask_on} {0}
#variable {recently_reclaimed} {0}
#variable {mask_check} {0}
#variable {mask_glyph_state} {unknown}

#alias {mask}
{
    #switch {"%1"} {
        #case "on" {
            #variable {mask_on} {1};
            #echo {<ecf>You will use your mask only. Glyph usage is automatic.<088>};
			#if {"$mask_glyph_state" == "unknown" || "$mask_glyph_state" == "on"} {
				#nop make sure the glph is off as the default mask on state;
				#send !trace glyph
			}
        };
        #case "off" {
            #variable {mask_on} {0};
            #echo {<ecf>You will not use any masks, and will reclaim instantly.<088>};
			#if {"$mask_glyph_state" == "unknown" || "$mask_glyph_state" == "on"} {
				#nop toggle the glyph to ensure it's off;
				#send !trace glyph
			}
        };
        #case "all" {
            #variable {mask_on} {2};
            #echo {<ecf>You will now use anyone's mask. Glyph usage is manual.<088>};
			#if {"$mask_glyph_state" == "unknown"} {
				#nop get the glyph state, will throw errors if you have no mask - ignore;
				#send !trace glyph;
				#send !trace glyph
			}
        };
        #default {
			#local {masktrigger} {};
			#local {maskglyph} {};
            #if {$mask_on == 1} {
                #variable {masktrigger} {You will trigger from your mask only}
            }; #elseif {$mask_on == 2} {
                #variable {masktrigger} {You will trigger from anyone's mask}
            }; #else {
                #variable {masktrigger} {You will use corpse(s) instantly,}
            };
			#echo {<ecf>%s and the glyph is %s.<088>} {$masktrigger} {$mask_glyph_state}
        }
    }
}

#nop use this alias to either manually use a corpse or from a script triggered on low GP
#alias {mu}
{
	#if {$recently_reclaimed == 1} {
		#echo {<ecf>You are still busy reclaiming!<088>}
	} {
		#if {$corpses[inventory] + $corpses[coffin] + $corpses[freezer] + $corpses[cooler]} {
			#variable recently_reclaimed 1;
			#if {$corpses[inventory] > 0} {
				#send !drop corpse;
				_guild_reclaim 1;
				#delay {3} {_finish_reclaim}
			} {
				#if {$mask_on != 0} {
					#variable {mask_check} {1};
					#if {$mask_on == 1} {#send !trace glyph};
					_select_corpse
				} {
					_select_corpse
					_guild_reclaim 1;
					#delay {3} {_finish_reclaim}
				}
			}
		}
	}
}

#action {^The mask shines brightly}
{
	#variable {mask_glyph_state} {on};
	#if {$mask_on == 0} {
		#nop mask is off, turn glyph off;
		#send !trace glyph
	}; #elseif {$mask_on == 1 && $mask_check == 0} {
		#nop mask is on, not reclaiming, turn glyph off;
		#send !trace glyph
	}
}

#action {^The mask emits a dull glow}
{
	#variable {mask_glyph_state} {off};
	#if {$mask_check == 1} {
		#nop the glyph should be active during a reclaim;
		#send !trace glyph
	}
}

#action {^{You keep \*?The mask of Gemino Mortis.*\(worn}}
{
	#nop automatically turn the mask on when you keep it and it's worn;
	mask on
}

#action {^You have no mask.}
{
	#variable {mask_on} {0};
	#variable {mask_glyph_state} {unknown}
}

#nop override this action from corpses.tin
#action {^{The coffin expels (a|your selected) corpse!}} {
	#if {$corpses[coffin] > 0} {
		#math {corpses[coffin]} {$corpses[coffin] - 1}
	} {
		#variable {corpses[coffin]} {0}
	};
	_cleanup_reclaim
} {1}

#nop override this action from corpses.tin
#action {^You shake the frame and out drops }
{
	#if {$corpses[freezer] > 0} {
		#math {corpses[freezer]} {$corpses[freezer] - 1}
	} {
		#variable {corpses[freezer]} {0}
	};
	_cleanup_reclaim
} {1}

#nop override this action from corpses.tin
#action {^You activate the Ultra-Cooler to retrieve }
{
	#math {corpses[cooler]} {$corpses[cooler] - 1};
	_cleanup_reclaim
} {1}

#action {^%1's eyes glow bright green upon the fallen}
{
	#local {lname} {};
	#format {lname} {%l} {%1};
	#if {$mask_check == 1 && (($mask_on == 1 && "$lname" == "$ses_name") || $mask_on == 2)} {
		#variable {mask_check} {0};
		#if {$mask_on == 1} {#send !trace glyph};
		_guild_reclaim 2;
		#delay {3} {_finish_reclaim}
	} {
		#variable {mask_check} {0}
	}
}

#action {^%1's eyes glow a sickly green upon the fallen}
{
	#local {lname} {};
	#format {lname} {%l} {%1};
	#if {$mask_check == 1 && (($mask_on == 1 && "$lname" == "$ses_name") || $mask_on == 2)} {
		#variable {mask_check} {0};
		#if {$mask_on == 1} {#send !trace glyph};
		_guild_reclaim 1;
		#delay {3} {_finish_reclaim}
	} {
		#variable {mask_check} {0}
	}
}

#alias {_finish_reclaim}
{
	#variable {recently_reclaimed} {0};
	#echo {<ecf>Finished reclaim.<088>}
}

#alias {_cleanup_reclaim}
{
	#delay {3} {
		#if {$mask_check} {
			#variable {mask_check} {0};
			#if {$mask_on == 1} {#send !trace glyph};
			#echo {<ecf>No mask triggered, reclaiming corpse...<088>};
			_guild_reclaim 1;
			#delay {3} {_finish_reclaim}
		}
	}
}

#alias {_select_corpse}
{
	#if {$corpses[freezer] > 0} {
		#send !deslab
	}; #elseif {$corpses[cooler] > 0} {
		#send !uncooler corpse
	}; #elseif {$corpses[coffin] > 0} {
		#send !unwrap
	}
}

#alias {_guild_reclaim}
{
	#nop add or update your guild-specific corpse handling here;
	#switch {"$guild"} {
		#case "gentech" {
			#if {%1 == 2} {
				recharge;reclaim;recharge;reclaim
			} {
				recharge;reclaim
			}
		};
		#case "necromancer" {
			#if {%1 == 2} {
				qtrance preserve get/absorb/preserve get/absorb
			} {
				qtrance preserve get/absorb
			}
		};
		#case "fremen" {
			#if {%1 == 2} {
				reclaim;reclaim
			} {
				reclaim
			}
		};
		#case "knight" {
			#if {%1 == 2} {
				bury corpse;bury corpse
			} {
				bury corpse
			}
		};
		#case "cyborg" {
			#if {%1 == 2} {
				decompose corpse;decompose corpse
			} {
				decompose corpse
			}
		};
		#case "changeling" {
			#if {%1 == 2} {
				absorb;absorb
			} {
				absorb
			}
		};
		#case "angel" {
			#if {%1 == 2} {
				disperse;disperse
			} {
				disperse
			}
		};
		#case "bard" {
			#if {%1 == 2} {
				perform dirge;perform dirge
			} {
				perform dirge
			}
		};
		#case "jedi" {
			#if {%1 == 2} {
				absorb energy;absorb energy
			} {
				absorb energy
			}
		};
		#case "monk" {
			#if {%1 == 2} {
				study corpse;study corpse
			} {
				study corpse
			}
		};
		#case "priest" {
			#if {%1 == 2} {
				offer corpse;offer corpse
			} {
				offer corpse
			}
		};
		#case "bladesinger" {
			#if {%1 == 2} {
				salute corpse;salute corpse
			} {
				salute corpse
			}
		};
		#case "mage" {
			#if {%1 == 2} {
				cast corpse blast;cast corpse blast
			} {
				cast corpse blast
			}
		};
		#default {
			#echo {** Unhandled guild type in mask.tin! **};
			#bell
		}
	}
}

#class {mask} {close}
