#class {ktour} {kill}
#class {ktour} {open}

#var kmove_num 0
#var start_posn 0
#var ktourpath {}
#var kpathsize 0

#var ktour6 {{1}{1 1} {2}{3 2} {3}{1 3} {4}{2 1} {5}{4 2} {6}{6 1} {7}{5 3} 
	      {8}{6 5} {9}{4 6} {10}{2 5} {11}{4 4} {12}{6 3} {13}{5 1} 
	      {14}{4 3} {15}{5 5} {16}{3 4} {17}{2 2} {18}{4 1} {19}{6 2} 
	      {20}{5 4} {21}{6 6} {22}{4 5} {23}{2 6} {24}{1 4} {25}{3 3} 
	      {26}{1 2} {27}{3 1} {28}{5 2} {29}{6 4} {30}{5 6} {31}{3 5} 
	      {32}{1 6} {33}{2 4} {34}{3 6} {35}{1 5} {36}{2 3}}

#var ktour8 {{1}{1 1} {2}{3 2} {3}{5 3} {4}{7 2} {5}{5 1} {6}{4 3} {7}{2 4} 
	     {8}{4 5} {9}{6 6} {10}{8 7} {11}{6 8} {12}{5 6} {13}{7 5} 
	     {14}{8 3} {15}{7 1} {16}{6 3} {17}{8 4} {18}{6 5} {19}{4 4} 
	     {20}{5 2} {21}{6 4} {22}{8 5} {23}{7 7} {24}{5 8} {25}{3 7} 
	     {26}{1 8} {27}{2 6} {28}{3 8} {29}{1 7} {30}{2 5} {31}{1 3} 
	     {32}{2 1} {33}{4 2} {34}{6 1} {35}{8 2} {36}{7 4} {37}{8 6} 
	     {38}{7 8} {39}{5 7} {40}{7 6} {41}{8 8} {42}{6 7} {43}{4 8} 
	     {44}{2 7} {45}{4 6} {46}{3 4} {47}{1 5} {48}{3 6} {49}{5 5} 
	     {50}{4 7} {51}{2 8} {52}{1 6} {53}{3 5} {54}{1 4} {55}{2 2} 
	     {56} {4 1} {57}{6 2} {58}{8 1} {59}{7 3} {60}{5 4} {61}{3 3} 
	     {62}{1 2} {63}{3 1} {64}{2 3}}

#alias {k6set %1} {
	#list ktour6 find {%1} {start_posn};
	#if {$start_posn == 0} {sysreport Incorrect position};
	#else {
		#var kmove_num $start_posn;
		#var ktourpath $ktour6;
		#var kpathsize 36
	};
	#36 kmove
}

#alias {k8set %1} {
        #list ktour8 find {%1} {start_posn};
        #if {$start_posn == 0} {sysreport Incorrect position};
        #else {
                #var kmove_num $start_posn;
                #var ktourpath $ktour8;
                #var kpathsize 64;
        };
        #64 kmove
}

#alias kmove {
	#math kmove_num {$kmove_num + 1};
	#if {$kmove_num == $start_posn} {sysreport Tour complete!};
	#else {
		#if {$kmove_num>$kpathsize} {#math kmove_num {$kmove_num - $kpathsize}};
		ride $ktourpath[$kmove_num];
	};
}

#alias {sysreport}
{
	#echo %1
}

#action {^The game of the {advanced |}knight's tour has ended.} {#class ktour_init load}

#act {=M= Sally, the tournament organizer <?>.} {mission ask;mission accept}

#act {** You have accepted the mission 'Knight's Tournament (Part {1|2})' **.} {d;s;e;play}
#act {** You have accepted the mission 'Knight's Tournament (Part {3|4})' **.} {d;s;e;e;play}
#act {** You have accepted the mission 'Knight's Tournament (Part {5|6})' **.} {d;s;e;e;e;play}

#act {{1|2|4|6})' : %1/%2. **} {#if {%1 < %2} {play}}
#act {Congratulations to Tournament Challenger for winning the axe challenge.} {play}
#act {You sign up to play axe challenge} {start;#5 throw}
#act {You sign up to play knight's tour} {start}
#act {You sign up to play advanced knight's tour} {start}

#act {** You have completed mission 'Knight's Tournament (Part {1|2})' **} {w;n;u;mission return;mission ask;mission accept}
#act {** You have completed mission 'Knight's Tournament (Part {3|4})' **} {w;w;n;u;mission return;mission ask;mission accept}
#act {** You have completed mission 'Knight's Tournament (Part 5)' **} {w;w;w;n;u;mission return;mission ask;mission accept}
#act {** You have completed mission 'Knight's Tournament (Part 6)' **} {w;w;w;n;u;mission return;#class ktour kill}

#class {ktour} {close}

#class {ktour_init} {kill}
#class {ktour_init} {open}

#action {%d  {\*|-} {\*|-} {\*|-} {\*|-} {\*|-} {\*|-} $} {
	#var game_line %0;
	#replace game_line { } {;};
	#list line_entries create $game_line;

	#var star_detected 0;
	#loop 2 7 i {
		#if {"$line_entries[$i]" == "*"} {
			#math kx {$i - 1};
			#var star_detected 1;
		};
	};

	#if {$star_detected == 1} {
		#class ktour_init save;
		#class ktour_init clear;
		k6set $kx $line_entries[1]
	}
}

#action {%d  {\*|-} {\*|-} {\*|-} {\*|-} {\*|-} {\*|-} {\*|-} {\*|-}} {
        #var game_line %0;
        #replace game_line { } {;};
        #list line_entries create $game_line;

        #var star_detected 0;
        #loop 2 9 i {
                #if {"$line_entries[$i]" == "*"} {
                        #math kx {$i - 1};
                        #var star_detected 1;
                };
        };

        #if {$star_detected == 1} {
                #class ktour_init save;
                #class ktour_init clear;
                k8set $kx $line_entries[1]
        }
}

#class {ktour_init} {close}
