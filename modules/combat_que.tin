#nop combat_que.tin
/*
    Usage:
        Add 'cq reset' to corpsetrig to remove the locked status on each kill
        Add some cue items, 'cq add name/rating/when/priority/repeat/cmd/condition'
        Start the que, 'cq on'.
        Manage the [ready] status by calling update_cooldown <name> <0/1> from actions, like the rod runes at the end of this file.

    Commands:
        cq                  - show the list
        cq ready            - show the list (ready-only lines)
        cq on               - enable the que
        cq off              - disable the que
        cq sort <column>    - sorts the list by the specified column (name|rating|when|ready|priority|command|condition|locked)
        cq disable <name>   - disable the specified que item
        cq enable <name>    - enable the specified que item
        cq add <args>       - add a que item, args are mandatory, except for condition
                              <name>/<rating>/<when>/<priority>/<repeat>/<cmd>/<condition>
        cq remove <name>    - remove the specified que item
    
    Injecting script:
        You can use script for the command, however you must not use variables directly; use the $:var_name format instead of $, see the example below.
    
    Que item options:
        name      - short name of the item
        rating    - number from 0 to 9 to indicate the dungeon/area difficulty this ability should only be used in, 0 = que ignored, 9 = max
        when      - start/high/med/low/end/any, first few rounds or at any round, high health, med health or low health
        priority  - 0-999 priority - the que is processed from highest priority to lowest
        repeat    - flag to indicate if the ability can be used more than once per fight: 1=repeatable, used whenever ready=1, 2=non-repeatable, locked on use
        cmd       - the command to send to the mud, can include some scripting elements
        condition - script to add a custom condition, e.g. '$:mobname' == 'brain'  Note $: instead of $ and ' instead of "        
        ready     - flag to indicate the ability is on/off cooldown: 1=ready, 2=on cd
        enabled   - flag to enable/disable the comabt que item manually
        locked    - managed internall, flag to lock the ability after the first use if it's non-repeatable, reset on kill

    <when> options:
        start: rounds < 5, health >= 90%
        low:   health >= 10%
        med:   health >= 50%
        high:  health >= 80%
        end:   health < 5%
        any:   any mob health or rounds number
    
    Todo:
        Investigate ways of resetting rating when leaving a dungeon and manually running around
        Que priority?
*/
#class {ratinglist} {kill}
#class {combat_que} {kill}
#class {combat_que} {open}

#variable {combat_que} {}
#variable {combat_que_on} {0}
#variable {cq_debug} {0}
#variable {combat_que_module} {1}
#variable {area_rating} {0}
#variable {cq2} {}
#variable {sort_by} {rating}

#ticker {combat_que} {cq run} {1}

#alias {cqload} {#read .tt/3k/modules/combat_que.tin}

#nop example que
#alias {cq_example}
{
    cq clear;
    cq add ewell/7/start/999/0/(energy well;
    cq add owl/8/end/0/0/touch owl rune;
    cq add raven/9/start/0/0/touch raven rune;
    cq add heal/1/any/0/1/(heal $:ses_name/$:my[hp][current] < 1000;
    cq add heal2/5/any/0/1/hmheal/@:health_check{1000};
    rate 8;
    cq on
}

#function {health_check}
{
    #if {$my[hp][current] < %1} {
        #variable {result} {1}
    } {
        #variable {result} {0}
    }
}

#alias {cq}
{
    #switch {"%1"} {
        #case "" {
            _showcombatque;
        };
        #case "add" {
            #regexp {%0} {{add ([a-zA-z0-9_]{1,12})\/(-?[0-9])\/(start|end|high|med|low|any)\/([0-9]{1,3})\/([0-9])\/([a-zA-Z0-9_\$\:='@\( ]+)(?:\/)?(.*)?}} {
                #nop cq add <name>/<rating>/<when>/<priority>/<repeat>/<cmd>/<condition>;
                #nop cq add   %2     %3       %4      %5         %6     %7       &8;

                #nop some housekeeping;
                #local {cdname} {};
                #format {cdname} {%l} {&2};
                #local {index} {@get_index{$cdname}};
                #local {condition} {&8};
                #replace {condition} {'} {"};

                #if {$index != 0} {
                    #nop delete the existing entry and replace it;
                    #list {combat_que} {delete} {$index}
                };

                #list {combat_que} {add} {{{name}{$cdname}{rating}{&3}{when}{&4}{priority}{&5}{repeat}{&6}{cmd}{&7}{condition}{$condition}{locked}{0}{ready}{1}{enabled}{1}}};
                #echo {<028>Combat que '$cdname' added.<088>}
            } {
                #echo {<028>Syntax is:\ncq add name/rating/when/priority/repeat/command/condition<088>}
            }
        };
        #case "remove" {
            #local {index} {@get_index{%2}};
            #if {$index > 0} {
                #echo {<028>Combat que '$combat_que[$index][name]' removed.<088>};
                #list {combat_que} {delete} {$index}
            } {
                #echo {<028>Combat que '%2' not found.<088>}
            }
        };
        #case "clear" {
            #variable {combat_que} {};
            #variable {combat_que_on} {0};
            #echo {<028>Combat que cleared.<088>}
        };
        #case "on" {
            #if {&{combat_que[]} != 0} {
                #variable {combat_que_on} {1};
                #echo {<028>Combat que enabled.<088>}
            } {
                #echo {<028>Combat que cannot be enabled because it's empty.<088>}
            }
        };
        #case "off" {
            #variable {combat_que_on} {0};
            #echo {<028>Combat que disabled.<088>}
        };
        #case "sort" {
            #regexp {%0} {{sort (name|rating|when|ready|priority|command|condition|locked)}} {
                #variable {sort_by} {&2};
                cq
            } {
                #echo {<028>Unknown sort option.<088>}
            }
        };
        #case "refresh" {
            #foreach {*combat_que[]} {index} {
                #variable {combat_que[$index][locked]} {0}
            };
            #if {$cq_debug} {#echo {CQ: refreshed}}
        };
        #case "enable" {
            #local {index} {@get_index{%2}};
            #if {$index > 0} {
                #variable {combat_que[$index][enabled]} {1};
                #echo {<028>Combat que '$combat_que[$index][name]' enabled.<088>}
            } {
                #echo {<028>Combat que '%2' not found.<088>}
            }
        };
        #case "disable" {
            #local {index} {@get_index{%2}};
            #if {$index > 0} {
                #variable {combat_que[$index][enabled]} {0};
                #echo {<028>Combat que '$combat_que[$index][name]' disabled.<088>}
            } {
                #echo {<028>Combat que '%2' not found.<088>}
            }
        };
        #case "debug" {
            #if {$cq_debug} {
                #variable {cq_debug} {0};
                #echo {<028>Combat que debug disabled.<088>}
            } {
                #variable {cq_debug} {1};
                #echo {<028>Combat que debug enabled.<088>}
            }
        };
        #case "help" {
            #echo {<028>Coming soon...<088>}
        };
        #case "ready" {
            _showcombatque ready;
        };
        #case "run" {
            #if {$combat_que_on == 1 && $mip[round] > 0 && $enemy[hp] > 0}
            {
                #list {combat_que} {indexate} {priority};
                #list {combat_que} {order};
                #list {combat_que} {reverse};

                #foreach {*combat_que[]} {index} {      
                    #if {$combat_que[$index][enabled] == 1} {
                        #if {@check_ready{$index} == 0} {
                            #if {$cq_debug} {#echo {CQ: $combat_que[$index][name] not ready}};
                            #continue
                        };
                       #if {@check_locked{$index} == 0} {
                            #if {$cq_debug} {#echo {CQ: $combat_que[$index][name] not executed: locked}};
                            #continue
                        };
                        #if {@check_rating{$index} == 0} {
                            #if {$cq_debug} {#echo {CQ: $combat_que[$index][name] not executed: rating: $combat_que[$index][rating] vs $area_rating}};
                            #continue
                        };      
                        #if {@check_when{$index} == 0} {
                            #if {$cq_debug} {#echo {CQ: $combat_que[$index][name] not executed: when: $combat_que[$index][when]locked}};
                            #continue
                        };        
                        #if {@check_condition{$index} == 0} {
                            #if {$cq_debug} {#echo {CQ: $combat_que[$index][name] not executed: condition not met}};
                            #continue
                        };                        
                        #nop passed all conditions, execute!;       
                        #if {$cq_debug} {#echo {CQ: Executing $combat_que[$index][name], cmd: $combat_que[$index][cmd]}};
                        @execute_index{$index}
                    }
                }
            }
        };
        #default {
            #echo {<028>Unknown command, use cq [add/remove/run/clear/debug/refresh/enable/disable]<088>}
        }
    }
}

#function {sort_colour}
{
    #if {"%1" == "$sort_by"} {
        #variable {result} {<488><128>}
    } {
        #variable {result} {<128>}
    }
}

#alias {_showcombatque}
{
    #local {cuelist} {};
    #format {cuelist} {<128> COMBAT QUE                                                           <088>%sEN<088> <128>AR:<028>%s<088>} {@check_colour{$combat_que_on}} {$area_rating} {$cuelist};
    #format {cuelist} {%s\n<028>==============================================================================<088>} {$cuelist};
    #format {cuelist} {%s\n<128> %sName<088>         %sRtg<088> %sWhen<088>  %sPri<088> %sRep<088> %sRdy<088> %sLck<088> %sCommand<088>              %sCondition<088><088>} {$cuelist}
        {@sort_colour{name}} {@sort_colour{rating}} {@sort_colour{when}} {@sort_colour{priority}} {@sort_colour{repeat}} {@sort_colour{ready}}
        {@sort_colour{locked}} {@sort_colour{command}} {@sort_colour{condition}};
    #format {cuelist} {%s\n<028>------------------------------------------------------------------------------<088>} {$cuelist};

    #if {&{combat_que[]} == 0} {
        #format {cuelist} {%s\n You have no configured combat que items} {$cuelist}
    } {
        #list {combat_que} {indexate} {$sort_by};
        #list {combat_que} {order};
        #if {"$sort_by" == "priority"} {
            #list {combat_que} {reverse}
        };
        #foreach {*combat_que[]} {index} {
            #local {showelement} {1};
            #if {"%1" == "ready" && ($combat_que[$index][ready] == 0 || @check_rating{$index} == 0)} {
                #local {showelement} {0}
            };
            #if {$showelement} {
                #format {cuelist} {%s\n %s%-12s<088> %s%-3d<088> %s%-5s<088> %-3d %-3d %s%-3d<088> %s%-3d<088> %-20s %s%s<088>} {$cuelist}
                    {@check_colour{@check_enabled{$index}}} {$combat_que[$index][name]}
                    {@check_colour{@check_rating{$index}}} {$combat_que[$index][rating]}
                    {@check_colour{@check_when{$index}}} {$combat_que[$index][when]}
                    {$combat_que[$index][priority]}
                    {$combat_que[$index][repeat]}
                    {@check_colour{@check_ready{$index}}} {$combat_que[$index][ready]}
                    {@check_colour{@check_locked{$index}}} {$combat_que[$index][locked]}
                    {$combat_que[$index][cmd]}
                    {@check_colour{@check_condition{$index}}} {$combat_que[$index][condition]};
            }
        }
    };
    #echo {$cuelist<088>}
}

#function {get_index}
{
    #if {&{combat_que[]} != 0} {
        #local {cdname} {};
        #format {cdname} {%l} {%1};
        #list {combat_que} {indexate} {name};
        #list {combat_que} {find} {$cdname} {result}
    } {
        #variable {result} {0}
    }
}

#function {execute_name}
{
    #local {index} {@get_index{%1}};
    #if {$index > 0} {
        #variable {result} {@execute_index{$index}}
    }
}

#function {execute_index}
{
    #if {$combat_que[%1][repeat] == 0} {
        #variable {combat_que[%1][locked]} {1}
    };
    #variable {exec} {$combat_que[%1][cmd]};
    #replace {exec} {'} {"};
    #replace {exec} {$:} {$};
    #replace {exec} {@:} {@};
    #variable {result} {$exec}
}

#function {check_enabled}
{
    #variable {result} {$combat_que[%1][enabled]}
}

#function {check_ready}
{
    #variable {result} {$combat_que[%1][ready]}
}

#function {check_locked}
{
    #if {$combat_que[%1][locked] == 1} {
        #variable {result} {0}
    } {
        #variable {result} {1}
    }
}

#function {check_rating}
{
    #variable {result} {0};

    #if {$combat_que[%1][rating] >= 0} {
        #nop positive rating, so check que rating > area rating;
        #if {$combat_que[%1][rating] <= $area_rating} {
            #variable {result} {1}
        }
    } {
        #nop negative que rating, so check that -1 * (que rating) < $area rating;
        #if {$combat_que[%1][rating] + $area_rating <= 0} {
            #variable {result} {1};
        }
    }
}

#function {check_when}
{
    #variable {result} {0};

    #switch {"$combat_que[%1][when]"} {
        #case "start" {
            #if {$mip[round] < 5 && $enemy[hp] >= 75} {
                #variable {result} {1}
            }
        };
        #case "low" {
            #if {$enemy[hp] >= 10} {
                #variable {result} {1}
            }
        };
        #case "med" {
            #if {$enemy[hp] >= 50} {
                #variable {result} {1}
            }
        };
        #case "high" {
            #if {$enemy[hp] >= 80} {
                #variable {result} {1}
            }
        };
        #case "end" {
            #if {$enemy[hp] <= 5} {
                #variable {result} {1}
            }
        };
        #case "any" {
            #variable {result} {1}
        };
        #default {
            #variable {result} {0}
        }
    }
}

#function {check_condition}
{
    #if {"$combat_que[%1][condition]" == ""} {
        #variable {result} {1}
    } {
        #variable {exec} {#if {$combat_que[%1][condition]} {#variable result 1} {#variable result 0}};
        #replace {exec} {$:} {$};
        #replace {exec} {@:} {@};
        #replace {exec} {'} {"};
        $exec;
    }
}

#function {check_colour}
{
    #if {%1 == 1 || "%1" == "ON"} {
		#variable {result} {<228>}
	} {
        #variable {result} {<218>}
    }
}

#alias {update_cooldown %1 %d}
{
    #local {index} {@get_index{%1}};
    #if {$index > 0} {
        #if {%2 == 0} {
            #nop abilitiy has been used;
            #variable {combat_que[$index][ready]} {0};
            #if {$cq_debug} {#echo {CQ: $combat_que[$index][name] coodown: $combat_que[$index][ready]}};
        } {
            #nop ability has come off cooldown, attempt to use it again if repeatable;
            #variable {combat_que[$index][ready]} {1};
            #if {$combat_que[$index][repeatable]} {
                @execute_index{$index};
                #if {$cq_debug} {#echo {CQ: $combat_que[$index][name] coodown: $combat_que[$index][ready], repeatable, executing}};
            } {
                #if {$cq_debug} {#echo {CQ: $combat_que[$index][name] coodown: $combat_que[$index][ready], non-repeatable}};
            }
        }
    }
}

/*
	Script to automatically change rating when entering dungeons using your preconfigured list of dungeon::ratings.
	The data is automatically saved to the file .tt/3k/char/<charname>-ratings.tin file.
	
	Suggested:
	Add guildsetup as the first command in the area's botpath variable add a guildsetup alias to your <guild>.tin file.
	Add 'rate run' to the alias.

	Commands:
	rate add <area> <rating>	- add/update scaler for the area
	rate remove <area>	        - remove the area from the list
	rate run					- change the scaler based on the configured setting and $bots[stepper]
	rate						- show a list of configured scalers
    rate <number>               - set a manual rating
*/

#class {ratinglist} {read} {.tt/char/$ses_name-ratings.tin}
#class {ratinglist} {size} {scalerssize};

#if {$scalerssize == 0} {
	#echo {Rating file not found, creating default.};
	#class {ratinglist} {kill};
	#class {ratinglist} {open};
	#variable {ratings} {};
	#class {ratinglist} {close};
	#class {ratinglist} {save};
	#class {ratinglist} {write} {.tt/char/$ses_name-ratings.tin};
}

#unvariable {scalerssize}
#variable {ratings_module} {1}

#alias {rate}
{
	#switch {"%1"} {
		#case "add" {
			#if {"%2" != "" && "%3" != "" && %3 >= 0 && %3 <= 9}
			{
				#variable {ratings[$guild][%2]} {%3};
				#class {ratinglist} {write} {.tt/char/$ses_name-ratings.tin};
				#echo {<028>Area %2 rating set to %3.<088>}
			} {
				#echo {<028>Syntax is 'rate add <stepper_name> <0-5>'.<088>}
			}
		};
		#case "remove" {
			#if {"%2" != ""} {
				#unvariable {ratings[$guild][%2]};
				#class {ratinglist} {write} {.tt/char/$ses_name-ratings.tin};
				#echo {<028>Area %2 rating removed.<088>}
			} {
				#echo {<028>Syntax is 'rate remove <stepper_name>'.<088>}
			}
		};
		#case "run" {
			#if {$ratings[$guild][$bots[stepper]]} {
				#variable {area_rating} {$ratings[$guild][$bots[stepper]]}
			} {
                #variable {area_rating} {0}
            };
            #echo {<028>Area rating set to $area_rating<088>}
		};
		#case "" {
            #local {header} {};
			#nop #format {header} {%n} {$guild};
            #local {cuelist} {};
            #format {cuelist} {<128> AREA RATINGS %-25s <028>AR:%s<088>} {$header} {$area_rating};
            #format {cuelist} {%s\n<028>=============================================<088>} {$cuelist};
            #format {cuelist} {%s\n<128> Rtg Area Name<088>} {$cuelist};
            #format {cuelist} {%s\n<028>---------------------------------------------<088>} {$cuelist};
    		#if {&{ratings[]} == 0} {
				#format {cuelist} {%s\n| No ratings set} {$cuelist}
			} {
				#foreach {*ratings[$guild][%*]} {name} {
					#format {cuelist} {%s\n  %d  %s} {$cuelist} {$ratings[$guild][$name]} {$name}
				}				
			};
            #echo {$cuelist};
		};
		#default {
			#math {area_rating} {%1};
            #if {$area_rating >= 0 && $area_rating <= 9} {
                #echo {<afc>Manual rating set to $area_rating.<088>}
            } {
                #echo {<afc>Invalid area rating, range is 0-9.<088>}
            }
		}
	}
}

/*
    Cooldown Tracking
*/
#nop A trigger for energy well already exists in chat_monitors.tin, override with a higher priortiy
#action {^A shimmering ball of energy appears magically when %w snap}
{
    update_chat <158>Energy Well Actived<088> by <138>%1<088>!;
    #if {"%1" == "you"} {
        update_cooldown ewell 0;
        #delay {energywell} {update_cooldown ewell 1} {1680}
    }
} {1}

#action {^You touch the %1 rune on the artifact and hear a soft *click*}
{
    #format {touching_rune} {%l} {%1}
}

#action {^Nothing happens after touching that activated rune}
{
    update_cooldown $touching_rune 0
}

#action {^Upon the %1, the %2 rune glows brightly again.}
{
    update_cooldown %2 1
}

#action {^   whispering a dark prayer to the provenance of an ancient god...}
{
    update_cooldown raven 0
}

#action {^A charge of emerald energy from the relic fractally crawls across your head}
{
    update_cooldown owl 0
}

#action {^A shimmering sphere of energy engulfs you}
{
    update_cooldown protection 0;
    #delay {cq_protection} {update_cooldown protection 1} {9720}
}

#action {^You tear apart %* defenses!}
{
    update_cooldown shred 0;
    #delay {cq_shred} {update_cooldown shred 1} {540}
}

#action {^You bathe %* in the healing powers of the outer planes}
{
    update_cooldown heal 0;
    #delay {cq_heal} {update_cooldown heal 1} {600}
}

#action {^You invoke the powers of life and death to surround and protect you}
{
    update_cooldown immortality 0;
    #nop in case of a complete nolifer;
    #delay {cq_immortality} {update_cooldown immortality 1} {267840}
}

#action {^Energy flashes down from the sky, frying}
{
    update_cooldown fry 0;
    #delay {cq_fry} {update_cooldown fry 1} {3960}
}

#class {combat_que} {close}
