#nop scalers.tin
/*
	Script to automatically change scaler when entering dungeons using your preconfigured list of dungeon::scalers.
	The data is automatically saved to the file .tt/3k/char/<charname>-bias.tin file.
	
	Suggested:
	Add guildsetup as the first command in the area's botpath variable add a guildsetup alias to your <guild>.tin file.
	Add 'scaler run' to the alias, e.g: #if {$scalers_module} {scaler run};

	To use the hardmode, modify your bot files and inside the #act {^=M= {.. add the following: #var hardmode $global_hardmode;
	And at the bottom of the #act {^=M= {.. add/change the following: #if {$mobhard} {#if {$hardmode} {_check_mob}} {_check_mob}}
	
	Commands:
	scaler add <area> <scaler> [hm:0/1] - add/update scaler for the area, hardmode is optional, 0 or 1
	scaler remove <area>				- remove the area from the list
	scaler enable						- enable automatic scaler changing (default)
	scaler disable						- disable automatic scaler changing
	scaler run							- change the scaler based on the configured setting and $bots[stepper]
	scaler								- show a list of configured scalers
	scaler <1-150>						- set the current scaler via the mud-side command
	hmode <on/off>						- manually turn global hardmode on/off
*/

#class {scalers} {kill}
#class {scalerlist} {kill}
#class {scalers} {open}

#class {scalerlist} {read} {.tt/char/$ses_name-scale.tin}
#class {scalerlist} {size} {scalerssize};

#if {$scalerssize == 0} {
	#echo {Scaler file not found, creating default.};
	#class {scalerlist} {kill};
	#class {scalerlist} {open};
	#variable {botscalers} {};
	#class {scalerlist} {close};
	#class {scalerlist} {save};
	#class {scalerlist} {write} {.tt/char/$ses_name-scale.tin};
	#unvariable {scalerssize}
}

#variable {scalers_module} {1}
#variable {scalers_enabled} {1}
#variable {global_hardmode} {1}

#function {hmonoff}
{
	#if {%1 == 1} {
		#variable {result} {<aea>ON<088>}
	} {
		#variable {result} {<eaa>OFF<088>}
	}
}

#alias {hmode on} {#variable global_hardmode 1}
#alias {hmode off} {#variable global_hardmode 0}

#alias {scaler}
{
	#switch {"%1"} {
		#case "add" {
			#if {"%2" != "" && "%3" != "" && %3 >= 1 && %3 <= 150}
			{
				#variable {botscalers[$guild][%2][scaler]} {%3};
				#if {"%4" == "OFF" || %4 == 0} {
					#variable {botscalers[$guild][%2][hardmode]} {0}
				} {
					#variable {botscalers[$guild][%2][hardmode]} {1}
				};
				#class {scalerlist} {write} {.tt/char/$ses_name-scale.tin};
				#echo {<fca>Dungeon %2 scaler set to %3 (hardmode=@hmonoff{$botscalers[$guild][%2][hardmode]}<fca>).<088>}
			} {
				#echo {<fca>Syntax is 'scaler add <name> <level(1-150)> [hardmode(0/1)]'.<088>}
			}
		};
		#case "remove" {
			#if {"%2" != ""} {
				#unvariable {botscalers[$guild][%2][scaler]};
				#class {scalerlist} {write} {.tt/char/$ses_name-scale.tin};
				#echo {<fca>Dungeon %2 scaler removed.<088>}
			} {
				#echo {<fca>Syntax is 'scaler remove <name>'.<088>}
			}
		};
		#case "on" {
			#variable {scalers_enabled} {1};
			#echo {<fca>Scalers enabled.<088>}
		};
		#case "off" {
			#variable {scalers_enabled} {0};
			#echo {<fca>Scalers disabled.<088>}
		};
		#case "run" {
			#if {$scalers_enabled && $botscalers[$guild][$bots[stepper]][scaler]} {
				#send !scaler $botscalers[$guild][$bots[stepper]][scaler];
				#variable {global_hardmode} {$botscalers[$guild][$bots[stepper]][hardmode]}
			};
		};
		#case "" {
			#local {header} {};
			#local {scaleout} {};
			#format {header} {[%n::%n]} {$ses_name} {$guild};
            #format {scaleout} {<afa> AREA SCALERS %-36s <088><afc>EN:%-3s <afc>HM:%-3s} {$header} {@hmonoff{$scalers_enabled}} {@hmonoff{global_hardmode}};
            #format {scaleout} {%s\n<afc>-----------------------------------------------------------------<088>} {$scaleout};
            #format {scaleout} {%s\n<afc> Scaler HM   Area Name<088>} {$scaleout};
            #format {scaleout} {%s\n<afc>-----------------------------------------------------------------<088>} {$scaleout};
			#foreach {*botscalers[$guild][%*]} {name} {
					#format {scaleout} {%s\n %-6d %-3s  %-20s} {$scaleout} {$botscalers[$guild][$name][scaler]} {@hmonoff{$botscalers[$guild][$name][hardmode]}} {$name}
			};
			#echo {$scaleout};
			#send !scaler;
		};
		#default {
			#send !scaler %1
		}
	}
}

#class {scalers} {close}
