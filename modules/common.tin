#nop common.tin
#nop include this file in your character file to enable common functionality between guilds.
#read {.tt/3k/modules/backpack.tin}
#read {.tt/3k/modules/onkill.tin}
#read {.tt/3k/modules/scalers.tin}
#read {.tt/3k/modules/combat_que.tin}
#read {.tt/3k/modules/mask.tin}
#read {.tt/3k/modules/watchdog.tin}

#variable {corpsetrig_on} {1}
#variable {freezer} {off}
#variable {resume_action} {.resume}

#nop change this to the room vnum that you usually anchor in
#variable {winchroom} {152}

#nop helper functions
#function {tolower}
{
	#format {result} {%l} {%1}
}
#function {statuscolour}
{
	#switch {%1} {
		#case 0 {
			#variable {result} {<108>}
		};
		#case 1 {
			#variable {result} {<028>}
		};
		#default {
			#variable {result} {<faf>}
		}
	}
}
#function {onoffcolour}
{
	#if {"%1" == "ON" || "%1" == "on"} {
		#variable {result} {<afa>%1<088>}
	} {
		#variable {result} {<faa>%1<088>}
	}
}

#nop @onoffnumbercolour{on/off;colouron;number}
#function {onoffnumbercolour}
{
	#if {"%1" == "ON" || "%1" == "on"} {
		#variable {result} {%2%3<088>}
	} {
		#variable {result} {<faa>%3<088>}
	}
}

#function {storedcorpses}
{
	#math {result} {$corpses[inventory] + $corpses[coffin] + $corpses[freezer] + $corpses[cooler] + $corpses[follower]}
}

#nop define on_char_reconnect & on_char_login in your character file for configuring defaults
#alias {on_char_login} { } {9}
#alias {on_guild_login} { } {9}
#alias {on_char_reconnect} { } {9}
#alias {on_guild_reconnect} { } {9}
#action {^3Kingdoms welcomes you back from linkdeath} {#delay {1} {on_reconnect}}
#action {^3Kingdoms welcomes you!} {#delay {1} {on_login}}
#alias {on_Login}
{
	on_char_login;
	#delay {1} {on_guild_login}
}
#alias {on_reconnect}
{
	on_char_reconnect;
	#delay {1} {on_guild_reconnect}
}

#action {^{You keep \*?Tech Ultra-Cooler.}} {#variable cooler on}
#action {^The freezer can hold no more corpses!} {#var corpses[freezer] 50}
#action {{You keep \*?Death\'s Freezer \((\d+) corpses\)\.}} {#variable corpses[freezer] %2;#variable freezer on} {1}
#action {^{There is no reason to \'{slab|deslab}\' here\.}}
{
	#variable freezer off;
	#variable corpses[freezer] 0
}

#nop the corpse trig action is defined here and will work before a stepper is run, once run it will override this action
#action {^=K=%1 dealt the killing blow to} {corpsetrig} {9}
#alias {cte} {#variable {corpsetrig_on} {1};#echo {<bfb>Corpse trigger enabled.<088>}}
#alias {ctp} {#variable {corpsetrig_on} {2};#echo {<bfb>Corpse trigger enabled (party).<088>}}
#alias {ctd} {#variable {corpsetrig_on} {0};#echo {<bfb>Corpse trigger disabled.<088>}}

#nop miscellaneous helpful stuff
#action {^Resistances:} {#echo {<028>                Edg   Blu   Fir   Ice   Acd   Ele   Psi   Nrg   Psn   Rad<088>}}
#action {^Everything slowly fades into blackness...$} {.stop}

#nop helpful client stuff
#alias {sideon} {redraw map on;redraw canvas on}
#alias {sideoff} {redraw map off;redraw canvas off}

#nop helpful rolm stuff
#alias {setwr} {#variable {winchroom} {$roomvnum}}
#action {^The light fades and you materialize elsewhere....}
{
    #if {"$winchroom" != ""} {#map goto $winchroom};
}
#action {^A small golden strand falls from the relic and imbeds itself into the ground.}
{
    #variable {winchroom} {$roomvnum};
}

#nop mythic knight's shield
#nop override these aliases by defining them elsewhere
#alias {on_kshield_equip} {#send !repeat combat/bash/0/0} {9}
#alias {on_kshield_remove} {#send !repeat off} {9}
#action {^Your training from the Grand Master Knight reminds you to train 'bash'} {on_kshield_equip;#gag {backward.$}} {9}
#action {^You carefully remove the shield} {on_kshield_remove;#ungag {backward.$}} {9}
#action {^There is no reason to 'bash' here} {on_kshield_remove} {9}

#nop lord stanley's cup
#nop override this alias by defining it elsewhere and setting the priority to {1}
#alias {on_hockey_fan} {#if {$mask_module} {guild_reclaim}} {9}
#action {^Out of nowhere a crazed hockey fan leaps towards the Cup} {on_hockey_fan} {9}

#nop supplements to 'cbrief on mo'
#gag {^%* tickled %* in the stomach}
#gag {^%* struck but did no damage}
#gag {^Your great speed allows}
#gag {^You swing the Sword of Spirit at your foe}
#gag {^You lash out with the Sword of Spirit}
#gag {^>$}
#gag {^Your party overwhelms the Beatle's defenses}

#nop helpful area aliases & actions
#alias {zhb1} {go zelligars;scaler 130;n;n;w;n;nw;e;n;e;e;s;s}
#alias {zhb2} {search shelves;get gloves;n;n;w;w;w;s;get key;n;e;e;e;s;s;e;e;e;n;e;e;e;n;n;n;n;w;w;w;w;s;s;s;s;unlock door;open door;w;get key;e;n;n;n;n;e;e;e;e;s;s;s;s;w;w;w;s;w;w;w;n;n;w;w;n;unlock door;open door;w;w;s;s;sw;w;w;w;n;nw;e;e;e;e;n;ne;e;e;e}
#alias {secz40} {#action {^Section 40 (ne,n,s)} {#map go 25088};#send !enter $sectionz[40];pause game;play;unpause game;botclear;ba sectionz40_short;cycle 1;#delay {0.5} {#map go 25088}}
#action {{Z|z} {R|r}eset} {#bell}


#action {^[PARTY] %1: {(d|D)ungeons\?}}
{
	#read {.tt/3k/modules/dungeonreport.tin}
}

#action {^[PARTY] %1: {(w|W)here are we\?}}
{
	#if {$bot == 1} {
		#send !ptell We are in $bots[stepper], room #$roomvnum, step $path_pos/$path_len. My target is at $enemy[hp]% health.
	}
}

#alias {roundstodays} {#local days 0;#math {days} {%1/1800.0/24.0};#echo {%d rounds is %f days.} {%1} {$days}}
#alias {daystorounds} {#local rounds 0;#math {rounds} {%1*1800.0*24.0};#echo {%f days is %d rounds.} {%1} {$rounds}}

#nop run to a room vnum
#alias {go2} {#map run %1}
