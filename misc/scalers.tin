#nop scalers.tin
/*
	Script to automatically change scaler when entering dungeons using your preconfigured list of dungeon::scalers.
	The data is automatically saved to the file .tt/3k/char/<charname>-bias.tin file.
	
	Suggested:
	Add guildsetup as the first command in the area's botpath variable add a guildsetup alias to your <guild>.tin file.
	Add 'scalers run' to the alias.

	Commands:
	scaler add <area> <scaler>	- add/update scaler for the area
	scaler remove <area>        - remove the area from the list
	scaler enable               - enable automatic scaler changing (default)
	scaler disable              - disable automatic scaler changing
	scaler run                  - change the scaler based on the configured setting and $bots[stepper]
  scaler <number>             - set the mud-side scaler value
	scaler                      - show a list of configured scalers
*/

#class {scalers} {kill}
#class {scalerlist} {kill}
#class {scalers} {open}

#class {scalerlist} {read} {.tt/char/$ses_name-scale.tin}
#class {scalerlist} {size} {scalerssize};

#if {$scalerssize == 0} {
	#echo {Scaler file not found, creating default.};
	#class {scalerlist} {kill};
	#class {scalerlist} {open};
	#variable {botscalers} {};
	#class {scalerlist} {close};
	#class {scalerlist} {save};
	#class {scalerlist} {write} {.tt/char/$ses_name-scale.tin};
	#unvariable {scalerssize}
}

#variable {scalers_module} {1}
#variable {scalers_enabled} {1}

#alias {scaler}
{
	#switch {"%1"} {
		#case "add" {
			#if {"%2" != "" && "%3" != "" && %3 >= 1 && %3 <= 150}
			{
				#variable {botscalers[$guild][%2]} {%3};
				#class {scalerlist} {write} {.tt/char/$ses_name-scale.tin};
				#echo {<fca>Dungeon %2 scaler set to %3.<088>}
			} {
				#echo {<fca>Syntax is 'scaler add <stepper_name> <level>'.<088>}
			}
		};
		#case "remove" {
			#if {"%2" != ""} {
				#unvariable {botscalers[$guild][%2]};
				#class {scalerlist} {write} {.tt/char/$ses_name-scale.tin};
				#echo {<fca>Dungeon %2 scaler removed.<088>}
			} {
				#echo {<fca>Syntax is 'scaler remove <stepper_name>'.<088>}
			}
		};
		#case "on" {
			#variable {scalers_enabled} {1};
			#echo {<fca>Scalers enabled.<088>}
		};
		#case "off" {
			#variable {scalers_enabled} {0};
			#echo {<fca>Scalers disabled.<088>}
		};
		#case "run" {
			#if {$scalers_enabled && $botscalers[$guild][$bots[stepper]]} {
				#send !scaler $botscalers[$guild][$bots[stepper]]
			};
		};
		#case "" {
			#local {scaleout} {<228> +--------------------------+<088>};
			#if {$scalers_enabled} {
				#format {scaleout} {%s\n<228> |<138> Configured Scalers  [on] <228>|<088>} {$scaleout}
			} {
				#format {scaleout} {%s\n<228> |<138> Configured Scalers [off] <228>|<088>} {$scaleout}
			};
			#format {scaleout} {%s\n<228> +--------------------------+<088>} {$scaleout};		
			#if {&{botscalers[]} == 0} {
				#format {scaleout} {%s\n<228> |<178>      No scalers set      <228>|<088>} {$scaleout}
			} {
				#foreach {*botscalers[$guild][%*]} {name} {
					#format {scaleout} {%s\n<228> |<148> %-3d - %-18s <088><228>|<088>} {$scaleout} {$botscalers[$guild][$name]} {$name}
				}				
			};
			#echo {%s\n<228> +--------------------------+<088>} {$scaleout};
			#send !scaler;
		};
		#default {
			#send !scaler %1
		}
	}
}

#class {scalers} {close}
